config {
  type: "incremental"
}

pre_operations {
  DECLARE latest_load_date DEFAULT (
    ${when(incremental(),
    `SELECT MAX(load_date) FROM ${ self() }`,
    `SELECT DATE("1000-01-01")`)}
  )
}

${when(incremental(),
  /******************************************
   INCREMENTAL SQL
   Default when executing:
     
     dataform run \
       --actions population_by_zip_2000_prod
  ******************************************/
  `
  SELECT
    load_date,
    zipcode,
    geo_id,
    minimum_age,
    maximum_age,
    gender,
    population,
  FROM ${ref("population_by_zip_2000")}
  WHERE
    load_date > latest_load_date
  `,
  when(dataform.projectConfig.vars.updateSchema,
  /**********************************************
   SCHEMA CHANGE SQL
   Execute by running:
     
     dataform run \
       --actions population_by_zip_2000_prod \
       --full-refresh \
       --vars=updateSchema=true
  **********************************************/
  `
  SELECT
    load_date,
    zipcode,
    geo_id AS geo_id_new_name,
    CAST(minimum_age AS STRING) AS minimum_age,
    maximum_age,
    gender,
    population,
  FROM ${self()}
  `,
  /*********************************************
   BULK LOAD SQL
   Execute by running:
     
     dataform run \
       --actions population_by_zip_2000_prod \
       --full-refresh
  *********************************************/
  `
  SELECT
    load_date,
    zipcode,
    geo_id,
    minimum_age,
    maximum_age,
    gender,
    population,
  FROM ${ref("population_by_zip_2000")}
  `))}